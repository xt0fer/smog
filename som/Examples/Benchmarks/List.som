"

$Id: List.som 31 2009-07-31 12:25:18Z michael.haupt $

Copyright (c) 2001-2007 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

List = Benchmark (

    benchmark = ( | result |
        result := self
            taklWithX: (self makeList: 15)
            withY: (self makeList: 10)
            withZ: (self makeList: 6).
        (result length = 10)
            ifFalse: [
                self error:
                    'Wrong result: ' + result length asString + ' should be: 10' ].
    )
    
    makeList: length = (
        (length = 0)
            ifTrue: [ ^nil ]
            ifFalse: [
                ^(ListElement new: length)
                    next: (self makeList: (length - 1)) ]
    )
    
    isShorter: x than: y = (
        | xTail yTail |
        
        xTail := x. yTail := y.
        [ yTail isNil ]
            whileFalse: [
                xTail isNil ifTrue: [ ^true ].
                xTail := xTail next.
                yTail := yTail next ].
        
        ^false
    )
    
    taklWithX: x withY: y withZ: z = (
        (self isShorter: y than: x)
            ifTrue: [
                ^(self
                    taklWithX: (self taklWithX: x next withY: y withZ: z)
                    withY: (self taklWithX: y next withY: z withZ: x)
                    withZ: (self taklWithX: z next withY: x withZ: y)) ]
            ifFalse: [ ^z ].
    )
    
)
