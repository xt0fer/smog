"
This benchmark is derived from richards.st, which is
part of Mario Wolczko's DeltaBlue and Richards collection.

License details:
  http://web.archive.org/web/20050825101121/http://www.sunlabs.com/people/mario/java_benchmarking/index.html
"
TaskState = RBObject (
  | packetPending taskWaiting taskHolding |

  isPacketPending = ( ^ packetPending )

  isTaskHolding = ( ^ taskHolding )

  isTaskWaiting = ( ^ taskWaiting )

  taskHolding: aBoolean   = ( taskHolding   := aBoolean )
  taskWaiting: aBoolean   = ( taskWaiting   := aBoolean )
  packetPending: aBoolean = ( packetPending := aBoolean )

  packetPending = (
      packetPending := true.
      taskWaiting := false.
      taskHolding := false
  )

  running = (
      packetPending := taskWaiting := taskHolding := false
  )

  waiting = (
      packetPending := taskHolding := false.
      taskWaiting := true
  )

  waitingWithPacket = (
      taskHolding := false.
      taskWaiting := packetPending := true
  )

  isTaskHoldingOrWaiting = ( ^ taskHolding or: [packetPending not and: [taskWaiting]] )

  isWaitingWithPacket = ( ^ packetPending and: [taskWaiting and: [taskHolding not]] )

  ----
  running = ( ^ super new running )

  waiting = ( ^ super new waiting )

  waitingWithPacket = ( ^ super new waitingWithPacket )
)
