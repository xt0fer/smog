JsonArray = JsonValue (
  | values |

  initialize = ( values := Vector new )

  addInteger: value = (
    values append: (JsonValue integer: value)
  )

  addDouble: value = (
    values append: (JsonValue double: value)
  )

  addBoolean: value = (
    values append: (JsonValue boolean: value)
  )

  addString: value = (
    values append: (JsonValue string: value)
  )

  add: value = (
    value ifNil: [ self error: 'value is null' ].
    values append: value
  )

  at: index putInteger: value = (
    values at: index put: (JsonValue integer: value)
  )

  at: index putDouble: value = (
    values at: index put: (JsonValue double: value)
  )

  at: index putBoolean: value = (
    values at: index put: (JsonValue boolean: value)
  )

  at: index putString: value = (
    values at: index put: (JsonValue string: value)
  )

  at: index put: value = (
    value ifNil: [ self error: 'value is null' ].
    values at: index put: value
  )

  removeAt: index = (
    values removeAt: index
  )

  size = (
    ^ values size
  )

  isEmpty = (
    ^ values isEmpty
  )

  at: index = (
    ^ values at: index
  )

  values = (
    ^ values
  )
 
  isArray = (
    ^ true
  )

  asArray = (
    ^ self
  )

  hashcode = (
    ^ values hashcode
  )

  = other = (
    self == other ifTrue: [ ^ true ].
    other == nil ifTrue: [ ^ false ].
    self class == other class ifFalse: [ ^ false ].

    ^ values = other values
  )

  ----
  
  new = ( ^ super new initialize )
  
  readFrom: string = (
    ^ (JsonValue readFrom: string) asArray
  )
)
